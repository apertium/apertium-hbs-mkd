# =========================================================================== #
#    Table of contents                                                        #
# =========================================================================== #
#    # Delimiters
#    # Lists 
#    ## Categories
#    ## Tag groups 
#    ## Lexicalised lists
#    # Sets
#    ## Boundary sets 
#    # Disambiguation 
#    ## NP-internal disambiguation
#    ## PP disambiguation
#    ## Verb chain disambiguation
#    ## Sentence level disambiguation
#    ## Lexicalised rules
#    # Syntactic function labelling
# =========================================================================== #

DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" ;

SOFT-DELIMITERS = "<,>" ;

# =========================================================================== #

SETS

LIST BOS = (>>>) ;
LIST EOS = (<<<) ;

LIST N = n ; 
LIST A = adj ; 
LIST V = vblex vbser ; 
LIST Adv = adv ; 
LIST Num = num ; 
LIST Det = det ; 
LIST Prop = np ;
LIST Pron = prn ; 
LIST Pr = pr ; 
LIST CS = cnjsub ;
LIST CC = cnjcoo ;
LIST Interr = itg ;
LIST Interj = ij ;
LIST Cm = (",") (cm) ;

# =========================================================================== #

LIST Clt = clt ; 
LIST Lp = lp ; 
LIST Opt = opt ; 

LIST Msc = m ma mi ;
LIST Fem = f ; 
LIST Neu = nt ; 

LIST Anim = ma ; 
LIST Inanim = mi ; 

LIST Nom = nom ; 
LIST Gen = gen ; 
LIST Dat = dat ; 
LIST Acc = acc ; 
LIST Voc = voc ;
LIST Loc = loc ; 
LIST Ins = ins ;

# =========================================================================== #

LIST GEN-PREP = "bez" "blizu" "čelo" "do" "duž" "ispred" "ispod" "iz" "iza" "između" "iznad" "izvan" "kod" "kraj" "mjesto" "nakon" "nakraj" "nasred" "navrh" "niže" "od" "oko" "osim" "pokraj" "ponad" "poput" "pozadi" "pored" "poslije" "poviše" "preko" "prije" "kasnije" "protiv" "put" "radi" "sred" "ukraj" "umjesto" "uoči" "uvrh" "uzduž" "van" "više" "vrh" "zarad" "zaradi" "zbog" "u" "za" "sa" "mimo" ;
LIST ACC-PREP = "kroz" "niz" "uz" "na" "o" "po" "u" "mimo" "među" "nad" "pod" "pred" "za"; 
LIST DAT-PREP = "k" "prema" "pri";
LIST LOC-PREP = "u" "na" "o" "po" "prema" "pri";
LIST INS-PREP = "s" "pred" "za" "nad" "pod" "među";
LIST ACC-OR-LOC-PREP = ACC-PREP | LOC-PREP ;

LIST V-COP = "biti" ; 

# =========================================================================== #

LIST GENDER = ma mi f nt mf mfn ;
LIST NUMBER = sg pl sp ;
LIST CASE = nom gen dat acc ins loc voc ;
LIST FIN-TEMPS = pres aor pii; 

# =========================================================================== #

SET NOT-ACC-LOC = CASE - Acc - Loc ; 

SET PRON-CLT = Pron + Clt ; 

SET V-FIN = FIN-TEMPS + V ;

# Common ambiguities 

SET A-OR-ADV = A | Adv ; 
SET ANIM-OR-INANIM = Anim | Inanim ; 

# All possible word categories
SET WORD = N | V | A | Pr | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") ;

# Categories that can come before the head noun in an NP 
SET PRE-N =  A | Det | Num | (n gen) | (prn gen) | CC ; 

SET MARK =  Cm | ("\\") | ("\;") ;

SET WORDMARK = WORD | MARK ;

# Categories which cannot be part of a noun phrase 
SET NPNH = WORDMARK - PRE-N ;
SET NPNHA = WORDMARK - PRE-N - Adv ;
SET NOT-ADV = WORDMARK - Adv ;

# Phrase member sets, and complements
SET NP-MEMBER = PRE-N | N | Pron ;
SET PP-MEMBER = NP-MEMBER | Pr ; 

SET NOT-NP-MEMBER = WORDMARK - NP-MEMBER ; 
SET NOT-PP-MEMBER = WORDMARK - PP-MEMBER ; 

# Categories which can be the head of a noun phrase
SET NP-HEAD = N | Pron | Prop ; 

SET S-BOUNDARY = CS | Interr | (n voc) ; 

# =========================================================================== #

SECTION
# =========================================================================== #
#   NP-internal disambiguation                                                #
# =========================================================================== #

REMOVE $$CASE IF (0C A-OR-ADV) (NOT 1* N + $$CASE BARRIER NPNHA) ;
    ## Šumski požar na hrvatskom otoku Kornati odnio još jedan ljudski život.

# =========================================================================== #
#   NP-agreement disambiguation                                               #
# =========================================================================== #

REMOVE $$NUMBER + Nom IF (1* V-FIN) (NOT 1* V-FIN + $$NUMBER) ;
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.

#REMOVE $$NUMBER + Nom IF (-1* V-FIN) (NOT -1* V-FIN + $$NUMBER) ; 
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.

SELECT NP-HEAD + $$NUMBER + Fem + Nom IF
                         (0 NP-HEAD + $$NUMBER + Fem)
                         (1* V-COP + $$NUMBER LINK 1 Lp + $$NUMBER + Fem BARRIER S-BOUNDARY) ;
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.
                                    
SELECT Num + $$GENDER IF (0 Num + $$GENDER) (1C NP-HEAD + $$GENDER);
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.
    
SELECT $$NUMBER + Nom IF (-1 BOS) (1C V-FIN + (iv) + $$NUMBER) ;
    ## Žene lete...
    
SELECT $$NUMBER + $$CASE IF (0C PRE-N) (1 NP-MEMBER + $$NUMBER + $$CASE) ;

SELECT $$NUMBER + $$CASE IF (0C NP-MEMBER) (-1 NP-MEMBER + $$NUMBER + $$CASE) ;

# =========================================================================== #
#   PP disambiguation                                                         #
# =========================================================================== #

REMOVE NOT-ACC-LOC IF (-1C* ACC-OR-LOC-PREP BARRIER NOT-PP-MEMBER) ;
    ## Šumski požar na hrvatskom otoku Kornati odnio još jedan ljudski život.

SELECT Gen IF (-1C GEN-PREP OR PRE-N + Gen );
SELECT Dat IF (-1C DAT-PREP OR PRE-N + Dat );
SELECT Acc IF (-1C ACC-PREP OR PRE-N + Acc );
SELECT Loc IF (-1C LOC-PREP OR PRE-N + Loc );
SELECT Ins IF (-1C INS-PREP OR PRE-N + Ins );

REMOVE Loc (NOT -1C LOC-PREP BARRIER BOS) ; 
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.


# =========================================================================== #
#   Verb chain disambiguation                                                 #
# =========================================================================== #

REMOVE (ref) IF (NOT -1* (prn ref) BARRIER MARK) (NOT 1* (prn ref) BARRIER MARK);
    ## Verb is not reflexive if there isn't a reflexive pronoun somewhere around it
    ## Though around doesn't necessarily mean till the first MARK
    
SELECT (ref) IF (1C (prn ref));

REMOVE PRON-CLT (0 PRON-CLT OR V-FIN)  (1C Lp OR Opt) ; 
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.

REMOVE Opt (0C Lp OR Opt) (-1C V-FIN) ;
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.


# =========================================================================== #
#   Sentence level disambiguation                                             #
# =========================================================================== #


SELECT Lp + $$GENDER + $$NUMBER IF
                     (-1C* NP-HEAD + $$GENDER + $$NUMBER BARRIER S-BOUNDARY) ; 
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.


SECTION
# =========================================================================== #
#   Syntactic function labelling                                              #
# =========================================================================== #

