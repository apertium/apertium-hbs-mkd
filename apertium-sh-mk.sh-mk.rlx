# =========================================================================== #
#    Table of contents                                                        #
# =========================================================================== #
#    # Delimiters
#    # Lists 
#    ## Categories
#    ## Tag groups 
#    ## Lexicalised lists
#    # Sets
#    ## Boundary sets 
#    # Disambiguation 
#    ## NP-internal disambiguation
#    ## PP disambiguation
#    ## Verb chain disambiguation
#    ## Sentence level disambiguation
#    ## Lexicalised rules
#    # Syntactic function labelling
# =========================================================================== #

DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" ;

SOFT-DELIMITERS = "<,>" ;

# =========================================================================== #

SETS

LIST BOS = (>>>) ;
LIST EOS = (<<<) ;

LIST N = n ; 
LIST A = adj ; 
LIST V = vblex vbser vbhaver vbmod ; 
LIST Adv = adv ; 
LIST Num = num ; 
LIST Det = det ; 
LIST Prop = np ;
LIST Pron = prn ; 
LIST DemPron = prn dem ;
LIST Pr = pr ; 
LIST CS = cnjsub ;
LIST CC = cnjcoo ;
LIST Interr = itg ;
LIST Interj = ij ;
LIST Cm = (",") (cm) ;

# =========================================================================== #

LIST Clt = clt ; 
LIST Lp = lp ; 
LIST Opt = opt ; 

LIST Msc = m ma mi ;
LIST Fem = f ; 
LIST Neu = nt ; 

LIST Ant = ant ; 
LIST Cog = cog ; 

LIST Anim = ma ; 
LIST Inanim = mi ; 

LIST Nom = nom ; 
LIST Gen = gen ; 
LIST Dat = dat ; 
LIST Acc = acc ; 
LIST Voc = voc ;
LIST Loc = loc ; 
LIST Ins = ins ;

# =========================================================================== #

LIST GEN-PREP = "bez"i "blizu"i "čelo"i "do"i "duž"i "ispred"i "ispod"i "iz"i "iza"i "između"i "iznad"i "izvan"i "kod"i "kraj"i "mjesto"i "nakon"i "nakraj"i "nasred"i "navrh"i "niže"i "od"i "oko"i "osim"i "pokraj"i "ponad"i "poput"i "pozadi"i "pored"i "poslije"i "poviše"i "preko"i "prijee"i "kasnije"i "protiv"i "put"i "radi"i "sred"i "ukraj"i "umjesto"i "uoči"i "uvrh"i "uzduž"i "van"i "više"i "vrh"i "zarad"i "zaradi"i "zbog"i "u"i "za"i "sa"i "mimo"i ;
LIST ACC-PREP = "kroz"i "niz"i "uz"i "na"i "o"i "po"i "u"i "mimo"i "među"i "nad"i "pod"i "pred"i "za"i; 
LIST DAT-PREP = "k"i ;
LIST LOC-PREP = "u"i "na"i "o"i "po"i "prema"i "pri"i;
LIST INS-PREP = "s"i "pred"i "za"i "nad"i "pod"i "među"i;
LIST ACC-OR-LOC-PREP = ACC-PREP | LOC-PREP ;

LIST V-COP = "biti"i ; 

LIST DualNum = "dva" "tri" "četiri" ;

# =========================================================================== #

LIST GENDER = ma mi f nt mf mfn ;
LIST NUMBER = sg pl sp ;
LIST CASE = nom gen dat acc ins loc voc ;
LIST FIN-TEMPS = pres aor pii; 

# =========================================================================== #

SET NOT-ACC-LOC = CASE - Acc - Loc ; 

SET PRON-CLT = Pron + Clt ; 

SET V-FIN = FIN-TEMPS + V ;

# Common ambiguities 

SET A-OR-ADV = A | Adv ; 
SET ANIM-OR-INANIM = Anim | Inanim ; 

# All possible word categories
SET WORD = N | V | A | Pr | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") ;

# Categories that can come before the head noun in an NP 
SET PRE-N =  A | Det | Num | (n gen) | (prn gen) | CC | (DemPron); 
SET ATTR = A | Num | (DemPron);

SET MARK =  Cm | ("\\") | ("\;") ;

SET WORDMARK = WORD | MARK ;

# Categories which cannot be part of a noun phrase 
SET NPNH = WORDMARK - PRE-N ;
SET NPNHA = WORDMARK - PRE-N - Adv ;
SET NOT-ADV = WORDMARK - Adv ;

# Phrase member sets, and complements
SET NP-MEMBER = PRE-N | N | Pron ;
SET PP-MEMBER = NP-MEMBER | Pr ; 

SET NOT-NP-MEMBER = WORDMARK - NP-MEMBER ; 
SET NOT-PP-MEMBER = WORDMARK - PP-MEMBER ; 

# Categories which can be the head of a noun phrase
SET NP-HEAD = N | Pron | Prop ; 

SET S-BOUNDARY = CS | Interr | (n voc) ; 

# =========================================================================== #

SECTION
# =========================================================================== #
#   NP-internal disambiguation                                                #
# =========================================================================== #

## REMOVE $$CASE IF (0C A-OR-ADV) (NOT 1* N + $$CASE BARRIER NPNHA) ;
    ## Šumski požar na hrvatskom otoku Kornati odnio još jedan ljudski život.

SELECT $$GENDER IF (-1C Ant + $$GENDER) (0 Cog + $$GENDER) ;
SELECT $$NUMBER IF (-1C Ant + $$NUMBER) (0 Cog + $$NUMBER) ;
REMOVE $$CASE IF (NOT -1 Ant + $$CASE) (0 Cog + $$CASE) ;
    ## Njihov kolega Nemanja Ružić pao je na testu letenja na trećoj godini akademije i okrenut će se jednom od pet ostalih akademskih programa.

# =========================================================================== #
#   NP-agreement disambiguation                                               #
# =========================================================================== #

REMOVE $$NUMBER + Nom IF (1* V-FIN) (NOT 1* V-FIN + $$NUMBER) ;
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.

#REMOVE $$NUMBER + Nom IF (-1* V-FIN) (NOT -1* V-FIN + $$NUMBER) ; 
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.

SELECT NP-HEAD + $$NUMBER + Fem + Nom IF
                         (0 NP-HEAD + $$NUMBER + Fem)
                         (1* V-COP + $$NUMBER LINK 1 Lp + $$NUMBER + Fem BARRIER S-BOUNDARY) ;
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.
                                    
SELECT Num + $$GENDER IF (0 Num + $$GENDER) (1C NP-HEAD + $$GENDER);
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.
    
SELECT $$GENDER IF (1C NP-HEAD + $$GENDER OR A + $$GENDER);
SELECT $$NUMBER IF (1C NP-HEAD + $$NUMBER OR A + $$NUMBER);
    
## SELECT $$NUMBER + $$CASE IF (0C NP-MEMBER) (-1 NP-MEMBER + $$NUMBER + $$CASE) ;

SELECT PRE-N + $$GENDER IF (1C* NP-HEAD + $$GENDER BARRIER NOT-NP-MEMBER);
    ## Select the gender of a noun modifier according to the next
    ## possible NP head to the right. We allow it to scan by any NP-member
    
SELECT $$NUMBER + Nom IF (-1 BOS) (1C V-FIN + (iv) + $$NUMBER) ;
    ## Žene lete...

SELECT $$NUMBER IF (-1 PRE-N + $$NUMBER) ;
    
SELECT Acc IF (-1C V + (tv) );

REMOVE Voc IF (NOT 1 Voc or Cm);

SELECT Gen IF (-1 DualNum + Nom);

SELECT Gen IF (1 DualNum + Nom) (0 Num + (ord) );

SELECT (iv) IF (1 Nom);

# =========================================================================== #
#   PP disambiguation                                                         #
# =========================================================================== #

REMOVE NOT-ACC-LOC IF (-1C* ACC-OR-LOC-PREP BARRIER NOT-PP-MEMBER) ;
    ## Šumski požar na hrvatskom otoku Kornati odnio još jedan ljudski život.

SELECT Nom IF (-1C ATTR + Nom);
SELECT Gen IF (-1C GEN-PREP OR ATTR + Gen);
SELECT Dat IF (-1C DAT-PREP OR ATTR + Dat);
SELECT Acc IF (-1C ACC-PREP OR ATTR + Acc);
SELECT Loc IF (-1C LOC-PREP OR ATTR + Loc);
SELECT Ins IF (-1C INS-PREP OR ATTR + Ins);

REMOVE Acc IF (NOT -1 V + (tv) );

# =========================================================================== #
#   Verb chain disambiguation                                                 #
# =========================================================================== #

REMOVE (ref) IF (NOT -1* (prn ref) BARRIER MARK) (NOT 1* (prn ref) BARRIER MARK);
    ## Verb is not reflexive if there isn't a reflexive pronoun somewhere around it
    ## Though around doesn't necessarily mean till the first MARK
    
SELECT (ref) IF (1C (prn ref));
    ## This is not enough for all reflexive verbs

REMOVE PRON-CLT (0 PRON-CLT OR V-FIN)  (1C Lp OR Opt) ; 
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.

REMOVE PRON-CLT (0 PRON-CLT OR V-FIN) (NOT -1 V + (tv)) (NOT 1* V + (tv) BARRIER MARK);

REMOVE Opt (0C Lp OR Opt) (-1C V-FIN) ;
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.
SELECT (vbser) IF (-1 (lp) );
    ## This is perhaps fallable

# =========================================================================== #
#   Sentence level disambiguation                                             #
# =========================================================================== #

SELECT (part) IF (0 (<"da">) ) (1 V);

REMOVE (itg) IF (NOT 1* ("<?>") ) (NOT -1* (part itg) ) (NOT 1* (part itg) );
    ## For direct questions

SELECT Lp + $$GENDER + $$NUMBER IF
                     (-1C* NP-HEAD + $$GENDER + $$NUMBER BARRIER S-BOUNDARY) ; 
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.


SECTION
# =========================================================================== #
#   Syntactic function labelling                                              #
# =========================================================================== #

