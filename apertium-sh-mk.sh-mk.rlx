# Authors: Hrvoje Peradin, Francis Tyers
# email: hperadin@gmail.com
# =========================================================================== #
#    Table of contents                                                        #
# =========================================================================== #
#    # Delimiters
#    # Lists 
#    ## Categories
#    ## Tag groups 
#    ## Lexicalised lists
#    # Sets
#    ## Boundary sets 
#    # Disambiguation 
#    ## NP-internal disambiguation
#    ## PP disambiguation
#    ## Verb chain disambiguation
#    ## Sentence level disambiguation
#    ## Lexicalised rules
#    # Syntactic function labelling
# =========================================================================== #

DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" ;

SOFT-DELIMITERS = "<,>" ;

# =========================================================================== #

SETS

LIST BOS = (>>>) ;
LIST EOS = (<<<) ;

LIST N = n ; 
LIST A = adj ; 
LIST V = vblex vbser vbhaver vbmod ; 
LIST Adv = adv ; 
LIST Num = num ; 
LIST Det = det ; 
LIST Prop = np ;
LIST Pron = prn ; 
LIST DemPron = prn dem ;
LIST Pr = pr ; 
LIST CS = cnjsub ;
LIST CC = cnjcoo ;
LIST Interr = itg ;
LIST Interj = ij ;
LIST Cm = (",") (cm) ;

# =========================================================================== #

LIST Clt = clt ; 
LIST Lp = lp ; 
LIST Opt = opt ; 

LIST Msc = m ma mi ;
LIST Fem = f ; 
LIST Neu = nt ; 

LIST Ant = ant ; 
LIST Cog = cog ; 

LIST Nomen = ant cog n np prn adj;

LIST Anim = ma ; 
LIST Inanim = mi ; 

LIST Nom = nom ; 
LIST Gen = gen ; 
LIST Dat = dat ; 
LIST Acc = acc ; 
LIST Voc = voc ;
LIST Loc = loc ; 
LIST Ins = ins ;

# =========================================================================== #

LIST GEN-PREP = "bez"i "blizu"i "čelo"i "do"i "duž"i "ispred"i "ispod"i "iz"i "iza"i "između"i "iznad"i "izvan"i "kod"i "kraj"i "mjesto"i "nakon"i "nakraj"i "nasred"i "navrh"i "niže"i "od"i "oko"i "osim"i "pokraj"i "ponad"i "poput"i "pozadi"i "pored"i "poslije"i "poviše"i "preko"i "prijee"i "kasnije"i "protiv"i "put"i "radi"i "sred"i "ukraj"i "umjesto"i "uoči"i "uvrh"i "uzduž"i "van"i "više"i "vrh"i "zarad"i "zaradi"i "zbog"i "u"i "za"i "sa"i "mimo"i ;
LIST ACC-PREP = "kroz"i "niz"i "uz"i "na"i "o"i "po"i "u"i "mimo"i "među"i "nad"i "pod"i "pred"i "za"i; 
LIST DAT-PREP = "k"i ;
LIST LOC-PREP = "u"i "na"i "o"i "po"i "prema"i "pri"i;
LIST INS-PREP = "s"i "pred"i "za"i "nad"i "pod"i "među"i;
LIST ACC-OR-LOC-PREP = ACC-PREP | LOC-PREP ;

LIST V-COP = "biti"i ; 

LIST DualNum = "dva"i "tri"i "četiri"i ;

# =========================================================================== #

LIST GENDER = ma mi f nt mf mfn ;
LIST NUMBER = sg pl sp ;
LIST CASE = nom gen dat acc ins loc voc ;
LIST FIN-TEMPS = pres aor pii; 

# =========================================================================== #

SET NOT-ACC-LOC = CASE - Acc - Loc ; 

SET PRON-CLT = Pron + Clt ; 

SET V-FIN = FIN-TEMPS + V ;

# Common ambiguities 

SET A-OR-ADV = A | Adv ; 
SET ANIM-OR-INANIM = Anim | Inanim ; 

# All possible word categories
SET WORD = N | V | A | Pr | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") ;

# Categories that can come before the head noun in an NP 
SET PRE-N =  A | Det | Num | (n gen) | (prn gen) | CC | (DemPron); 
SET ATTR = A | Num | (DemPron) | (prn pos) | (prn ind);

SET MARK =  Cm | ("\\") | ("\;") ;

SET WORDMARK = WORD | MARK ;

# Categories which cannot be part of a noun phrase 
SET NPNH = WORDMARK - PRE-N ;
SET NPNHA = WORDMARK - PRE-N - Adv ;
SET NOT-ADV = WORDMARK - Adv ;

# Phrase member sets, and complements
SET NP-MEMBER = PRE-N | N | Pron ;
SET PP-MEMBER = NP-MEMBER | Pr ; 

SET NOT-NP-MEMBER = WORDMARK - NP-MEMBER ; 
SET NOT-PP-MEMBER = WORDMARK - PP-MEMBER ; 

# Categories which can be the head of a noun phrase
SET NP-HEAD = N | Pron | Prop ; 

SET S-BOUNDARY = CS | Interr | (n voc) | EOS; 

# =========================================================================== #

SECTION
# =========================================================================== #
#   NP-internal disambiguation                                                #
# =========================================================================== #

## REMOVE $$CASE IF (0C A-OR-ADV) (NOT 1* N + $$CASE BARRIER NPNHA) ;
    ## Šumski požar na hrvatskom otoku Kornati odnio još jedan ljudski život.

##SELECT:Ant-Cog $$GENDER + $$NUMBER + $$CASE IF (-1C Ant + $$GENDER + $$NUMBER + $$CASE) (0 Cog + $$GENDER + $$NUMBER + $$CASE) ;
##SELECT:Ant-Cog $$GENDER + $$NUMBER + $$CASE IF (1C Ant + $$GENDER + $$NUMBER + $$CASE) (0 Cog + $$GENDER + $$NUMBER + $$CASE) ;
SELECT:Ant-Cog $$NUMBER IF (-1C Ant + $$NUMBER) (0 Cog + $$NUMBER) ;
## SELECT:Ant-Cog $$NUMBER IF (1C Ant + $$NUMBER) (0 Cog + $$NUMBER) ;
SELECT:Ant-Cog $$GENDER IF (-1C Ant + $$GENDER) (0 Cog + $$GENDER) ;
## SELECT:Ant-Cog $$CASE IF (1C Ant + $$CASE) (0 Cog + $$CASE) ;
    ## The cogname takse the number, gender and case of the name

#REMOVE:Ant-noPl (pl) IF (0 Ant + (pl) + (f) ) (NOT -1 ATTR + (pl));
#REMOVE:Cog-noPl (pl) IF (0 Cog + (pl) + (f) ) (NOT -1 ATTR + (pl));
# These two can be generalised to the following, probably:
REMOVE:np-noPl (pl) IF (0 (np) + (pl) + (f) ) (NOT -1 ATTR + (pl));

SELECT:CogNomF Nom IF 
    (0 Cog + (f)) 
    (NOT -1 Ant + ($$Case - Nom))
    (NOT 1 Ant + ($$Case - Nom));

#REMOVE (pl) IF (0C Nomen)(NOT -1C Nomen + (pl) );

REMOVE $$CASE IF (NOT -1 Ant + $$CASE) (0 Cog + $$CASE) ;
    ## Njihov kolega Nemanja Ružić pao je na testu letenja na trećoj godini akademije i okrenut će se jednom od pet ostalih akademskih programa.

SELECT:AdvV Adv IF (0 A-OR-ADV) ( 1C V ) ;

# =========================================================================== #
#   NP-agreement disambiguation                                               #
# =========================================================================== #

REMOVE $$NUMBER + Nom IF (1C* V-FIN BARRIER MARK) (NOT 1C* V-FIN + $$NUMBER BARRIER MARK) ;
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.

#REMOVE $$NUMBER + Nom IF (-1* V-FIN) (NOT -1* V-FIN + $$NUMBER) ; 
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.

SELECT:Nom+Perfect NP-HEAD + $$NUMBER + $$GENDER + Nom IF
                         (0 NP-HEAD + $$NUMBER + $$GENDER)
                         (1* V-COP + $$NUMBER LINK 1 Lp + $$NUMBER + $$GENDER BARRIER S-BOUNDARY) ;
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.
                                    
SELECT:NumberGender Num + $$GENDER IF (0 Num + $$GENDER) (1C NP-HEAD + $$GENDER);
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.
    
SELECT:GenNumNext $$GENDER + $$NUMBER IF (1C NP-HEAD + $$GENDER + $$NUMBER OR A + $$GENDER + $$NUMBER);
#SELECT $$NUMBER IF (1C NP-HEAD + $$NUMBER OR A + $$NUMBER);
    
## SELECT $$NUMBER + $$CASE IF (0C NP-MEMBER) (-1 NP-MEMBER + $$NUMBER + $$CASE) ;

#SELECT PRE-N + $$GENDER IF (1C* NP-HEAD + $$GENDER BARRIER NOT-NP-MEMBER);
    ## Select the gender of a noun modifier according to the next
    ## possible NP head to the right. We allow it to scan by any NP-member
    
SELECT:BOS-Nom-Viv $$NUMBER + Nom IF (-1 BOS) (1C V-FIN + (iv) + $$NUMBER) ;
    ## Žene lete...

SELECT:AtrrNum->NNum $$NUMBER IF (0 N OR A)(-1 ATTR + $$NUMBER) ;

SELECT:Nabrajanje $$NUMBER + $$GENDER + $$CASE IF ( -1 Cm ) ( -2 ATTR + $$NUMBER + $$GENDER + $$CASE ) ;
    
SELECT:tvAcc Acc IF (-1C V + (tv) );

REMOVE:notVoc Voc IF (NOT -1 Voc OR Cm);
REMOVE:notVoc Voc IF (NOT 1 Voc OR Cm);
    ## It's not vocative if it hasn't a separate phrase

SELECT:DualGen Gen IF (-1 DualNum + Nom);
    ## Tri žene
SELECT:OrdDual $$GENDER IF (1 DualNum + $$GENDER) (0 Num + (ord) );
    ## Prve tri žene. Prve should be accusative, gender the same as žene.    
SELECT (iv) IF (1C Nom);
# =========================================================================== #
#   PP disambiguation                                                         #
# =========================================================================== #

REMOVE NOT-ACC-LOC IF (-1C* ACC-OR-LOC-PREP BARRIER NOT-PP-MEMBER) ;
    ## Šumski požar na hrvatskom otoku Kornati odnio još jedan ljudski život.

SELECT:ATTR-Nom Nom IF (-1C ATTR + Nom);
SELECT:Pr-Gen Gen IF (-1C GEN-PREP OR ATTR + Gen);
SELECT:Pr-Dat Dat IF (-1C DAT-PREP OR ATTR + Dat);
SELECT:Pr-Acc Acc IF (-1C ACC-PREP OR ATTR + Acc);
SELECT:Pr-Loc Loc IF (-1C LOC-PREP OR ATTR + Loc);
SELECT:Pr-Ins Ins IF (-1C INS-PREP OR ATTR + Ins);

SELECT Dat IF (NOT -1C (pr) ) (NOT -1C ATTR + Loc);
    ## If dative stands alone, it's never locative

REMOVE Gen IF (NOT -1 ATTR) (NOT 1 ATTR OR N);
    ## Experimental "...da i sama ima tremu..."

REMOVE:notAcc Acc IF (NOT -1 V + (tv) ) (NOT -1 PRE-N + Acc);
    ## If it's not a target of a transitive verb, it's not accusative
    ## And if there isn't a modifier that's already in accusative

# =========================================================================== #
#   Verb chain disambiguation                                                 #
# =========================================================================== #

REMOVE (ref) IF (NOT -1* (prn ref) BARRIER MARK) (NOT 1* (prn ref) BARRIER MARK);
    ## Verb is not reflexive if there isn't a reflexive pronoun somewhere around it
    ## Though around doesn't necessarily mean till the first MARK
    
SELECT (ref) IF (1C (prn ref));
    ## Okrenut će se

REMOVE:je+lp PRON-CLT (0 PRON-CLT OR V-FIN)  (1C Lp OR Opt) ; 
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.

REMOVE:notVBHaver PRON-CLT + Gen (0 ("<je>")) (NOT -1 (vbhaver) );

REMOVE PRON-CLT (0 PRON-CLT OR V-FIN) (NOT -1 V + (tv)) (NOT 1* V + (tv) BARRIER MARK);



REMOVE Opt (0C Lp OR Opt) (-1C (vbser) ) ; #TODO: sometimes not only vbser, but also some pronouns
SELECT Lp (0C Lp OR Opt) (1C (vbser) ) ;
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.

REMOVE (pl) IF (0 Lp) (-1 (vbser) + (sg) ) ; 
REMOVE (pl) IF (0 Lp)  (1 (vbser) + (sg) ) ;
    # This should be extended to some barrier

SELECT (vbser) IF (-1* (lp) );
SELECT (vbser) IF (1* (lp) BARRIER V - (lp) - (pprs) );
    ## This is perhaps fallable
    
REMOVE (tv) IF (NOT 1* Acc BARRIER V);
SELECT (tv) IF (1* Acc BARRIER V);

# =========================================================================== #
#   Sentence level disambiguation                                             #
# =========================================================================== #

SELECT:daPart (part) IF (0 ("<da>") ) (1 V);

REMOVE (itg) IF (NOT 1* ("<?>") ) (NOT -1* (part itg) ) (NOT 1* (part itg) );
    ## For direct questions
    ## This is ok, however, the article is not correctly written: "Hoću li biti primljena na akademiju ili ne; ne samo da sam letjela..."
    ## The ';' here should have been a '?'
    
REMOVE (imp) IF (NOT 1* ("<!>") BARRIER EOS);
    ## It's not imperative, if it doesn't end with '!'
    ## Probably not always, but it'll do for this article

SELECT:Imalievosubjekt Lp + $$GENDER + $$NUMBER IF
                     (-1C* NP-HEAD + $$GENDER + $$NUMBER BARRIER S-BOUNDARY) ; 
    ## Radovanović je ušla u povijest kao prva žena-pilot u Srbiji.


SECTION
# =========================================================================== #
#   Syntactic function labelling                                              #
# =========================================================================== #

